#!perl
# ABSTRACT: use National Geographic daily photo of the day as desktop background

use strict;
use autodie ':all';
use File::Path 'mkpath';
use Getopt::Long;
use POSIX 'strftime';
use Pod::Usage::CommandLine qw(GetOptions pod2usage);
use WWW::Mechanize;

my %opts = (
    prev            => 0,
    'download-only' => 0,
    dir             => "$ENV{HOME}/Pictures/National Geographic");

GetOptions(\%opts,
    'prev|p=i',
    'dir|d=s',
    'date=s',
    'download-only') or pod2usage;

my $url =
    'http://photography.nationalgeographic.com/photography/photo-of-the-day/';
my $mech    = WWW::Mechanize->new;
my $date    = $opts{date} || strftime('%Y-%m-%d', localtime);
my $picfile = download_picture();

if (-e $picfile) {
    unless ($opts{'download-only'}) {
        system qw(gconftool-2 --type string --set /desktop/gnome/background/picture_filename), $picfile;
        system qw(gconftool-2 --type string --set /desktop/gnome/background/picture_options zoom);
    }
}

# download source picture from National Geographic and save
sub download_picture {
    my $tempfile  = "$opts{dir}/${date}.jpg";

    return $tempfile if -e $tempfile;

    $mech->get($url);

    for (1 .. $opts{prev}) {
        $mech->follow_link(text_regex => qr/previous/i);
    }

    my $link = $mech->find_link(text_regex => qr/download wallpaper/i)
        or die "failed to find wallpaper download link";

    unless (-d $opts{dir}) {
        mkpath $opts{dir};
    }

    $mech->get($link, ':content_file' => $tempfile);

    return $tempfile;
}

1;

__END__

=head1 SYNOPSIS

ng-daily-wallpaper [options]

    Options:
        --help          display this help message
        --dir           directory where images are saved
        --date          use previously downloaded picture for this date
        --prev          get Nth previoius picture of the day (up to 3)
        --download-only only download picture (do not set as wallpaper)

=head1 DESCRIPTION

This program downloads the current picture of the day from National Geographic,
resizes a copy to fit your monitor, and sets it as your desktop background.

=head1 OPTIONS

=over 4

=item B<--date>

Use the given date.  If a picture was already downloaded for that date, then
that picture will be used.  Otherwise the current picture will be downloaded
and saved for the given date.

=item B<--dir, -d>

Set directory where images are saved.

default: C<$HOME/Pictures/National Geographic>

=item B<--download-only>

Just download the picture and resize it.  Do not set as desktop wallpaper

=item B<--prev, -p>

Get Nth previous picture of the day. This may not work for large values.
Download links are not always available on older pictures of the day.

Default: 0 (get current day's picture)

=back
